@name 0xChatSounds_v2.1
@persist [CHAT_SOUNDS PLAYER_PHRASES FILTER]:table [UPDATE_DELAY MAX_WORDS]:number [ROOT_URL]:string
@trigger none

#include "include/chatCmd"

if(first())
{
    #FILTER["STEAM_0:0:61857394", number] = 1 #Ave
    #FILTER["STEAM_0:1:156243915", number] = 1 #kentura
    
    ROOT_URL       = "https://ox.krakenpro.ru/gmod/chatsounds/"
    UPDATE_DELAY   = 200
    MAX_WORDS      = 15
    PLAYER_PHRASES = table()
    
    function string getSoundGroup(Phrase:string) {
        local Sound = CHAT_SOUNDS["triggers", table][Phrase, string]
        if(!Sound) {
            local NumPhrase = Phrase:toNumber()
            if(NumPhrase) {
                Sound = CHAT_SOUNDS["triggers", table][NumPhrase, string]
            }
        }
        return Sound
    }
    
    function array getTriggerSound(SGroup:string) {
        local Count = CHAT_SOUNDS["sounds", table][SGroup, table]:count()
        if(!Count) {
            local NumGroup = SGroup:toNumber()
            Count = CHAT_SOUNDS["sounds", table][NumGroup, table]:count()
            if(Count) {
                return CHAT_SOUNDS["sounds", table][NumGroup, table][randint(1, Count), table]:values()
            }
        } else {
            return CHAT_SOUNDS["sounds", table][SGroup, table][randint(1, Count), table]:values()
        }
        return array()
    }
    
    function void loadTriggers() {
        if(httpCanRequest()) {
            httpRequest(ROOT_URL+"sounds.json?t="+curtime())
        } else {
            print("Try again later :(")
        }
    }
    
    loadTriggers()
    
    regChatCmd("", "CMD_ChatSounds", CHAT_FLAG_ALL)
    runOnHTTP(1)
}

if(httpClk()) {
    local Tbl = jsonDecode(httpData())
    if(Tbl:count()) {
        CHAT_SOUNDS = Tbl
        runOnHTTP(0)
        
        #printTable(CHAT_SOUNDS["sounds", table])
        
        timer("_play_chat_sounds", 0)
    } else {
        print("Loading Fail :(")
    }
}

if(clk("CMD_ChatSounds")) {
    local Caller = getCmdCaller()
    if(!FILTER[Caller:steamID(), number]) {
        local Message = getCmdArgs()
        
        local Sounds = array()
        local Phrase = ValidSound  = ""
        local Count = Message:count() > MAX_WORDS ? MAX_WORDS : Message:count()
        
        local J = I = 1
        while(I <= Count) {
            Phrase = ""
            if(Message:string(I) != "") {
                J = I
                while(J <= Count) {
                    local Msg = Message:string(J)
                    if(Msg != "") {
                        Phrase += (Phrase ? " " : "") + Msg
                        
                        local Sound = getSoundGroup(Phrase)
                        if(Sound) {
                            ValidSound = Sound
                            I = J
                        }
                    }
                    J++
                }
            }
            I++
            
            if(ValidSound) {
                Sounds:pushString(ValidSound)
                ValidSound = ""
            }
        }
        
        if(Sounds:count()) {
            local CallerId = Caller:id()
            if(!PLAYER_PHRASES:exists(CallerId)) {
                PLAYER_PHRASES[CallerId, table] = table("playtime" = 0, "phrases" = array())
            }
            
            PLAYER_PHRASES[CallerId, table]["playtime", number] = systime()
            PLAYER_PHRASES[CallerId, table]["phrases", array] = Sounds
        }
    }
}

if(clk("_play_chat_sounds")) {
    local CurTime = systime()
    foreach(K,V:number = PLAYER_PHRASES:keys()) {
        local CanPlayNext = CurTime > PLAYER_PHRASES[V, table]["playtime", number]
        if(CanPlayNext & PLAYER_PHRASES[V, table]["phrases", array]:count()) {
            local Phrase = PLAYER_PHRASES[V, table]["phrases", array]:removeString(1)
            #print(format("[%s]", Phrase))
            local Sound = getTriggerSound(Phrase)
            PLAYER_PHRASES[V, table]["playtime", number] = CurTime + Sound:number(2) + owner():ping()/1000 + entity(V):ping()/1000
            local Url = ROOT_URL+Sound:string(1)+".mp3"
            #print("load "+Url+"  "+V)
            soundURLload(V, Url, 1, 0, entity(V))
        }
    }
    timer("_play_chat_sounds", UPDATE_DELAY)
}